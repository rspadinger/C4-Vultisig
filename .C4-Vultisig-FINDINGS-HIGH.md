# H01 - ILOPool::claim : The amount transfered to the feeTaker is incorrect, which causes the claim function to revert 

## Risk: High

## Issue Type:

## Title: ILOPool::claim : The amount transfered to the feeTaker is incorrect, which causes the claim function to revert 

## Links: https://github.com/code-423n4/2024-06-vultisig/blob/cb72b1e9053c02a58d874ff376359a83dc3f0742/src/ILOPool.sol#L259

## Impact

When an eligible user calls the claim function, the following happens:

* the claimable liquidity (liquidity2Claim) is calculated according to the values specified in the vestingConfigs and the curren ttimestamp  
* the burn function is called on the Uniswap pool and the claimable liquidity is burned
* the project platform fee is deducted from the returned T0 and T1 amounts to adjust the amounts that will later be transferred to the msg.caller
* the accumulated Uniswap pool fees are calculated - for the claimable liquidity! 
* the performance fee is deducted from the returned T0 and T1 fee amounts 
* the adjusted fee amounts are added to the previously calculated amounts that will later be transferred to the msg.caller
* the position feeGrowthInside01LastX128 & liquidity values are updated for fee calculations on subsequent claim() calls
* the collect() function is called on the Uniswap pool => this transfer the burned T01 amounts and the TOTAL accumulated fees to the ILOPool !!! 
* the previously calculated TO1 + Fee01 amounts are transferred to the user
* **and finally, the ENTIRE left-over amounts for T01 are transferred to the feeTaker - this is WRONG !!!**

Important: When we call collect(), Uniswap transfers all accumulated fees to the ILOPool (not just the amount proportional to the burned liquidity). Proportional fee amounts (according to the burned liquidity) are sent to the user, however, ALL remaining Uniswap fees are sent to the feeTaker:

```
TransferHelper.safeTransfer(_cachedPoolKey.token0, feeTaker, amountCollected0 - amount0);
TransferHelper.safeTransfer(_cachedPoolKey.token1, feeTaker, amountCollected1 - amount1);
```

This means, when other users call the claim() functions and a sufficient amount of fees have been accumulated on the Uniswap pool (token swaps...), not enough tokens will be left on the ILOPool and the following calls will cause a revert:

```
TransferHelper.safeTransfer(_cachedPoolKey.token0, ownerOf(tokenId), amount0);
TransferHelper.safeTransfer(_cachedPoolKey.token1, ownerOf(tokenId), amount1);
```

At this stage, the protocol becomes unusable and users will no longer be able to claim their funds and pool earnings.
 

## Proof of Concept

Add the following code sections to the ILOPool.t.sol file.

We need the ISwapRouter interface. Either install and import the contracts/interfaces/ISwapRouter.sol or define an interface with the required functions at the top of the test file:

```
interface ISwapRouter {
    struct ExactInputSingleParams {
        address tokenIn;
        address tokenOut;
        uint24 fee;
        address recipient;
        uint256 deadline;
        uint256 amountIn;
        uint256 amountOutMinimum;
        uint160 sqrtPriceLimitX96;
    }

    struct ExactOutputSingleParams {
        address tokenIn;
        address tokenOut;
        uint24 fee;
        address recipient;
        uint256 deadline;
        uint256 amountOut;
        uint256 amountInMaximum;
        uint160 sqrtPriceLimitX96;
    }

    function exactInputSingle(ExactInputSingleParams calldata params) external payable returns (uint256 amountOut);

    function exactOutputSingle(ExactOutputSingleParams calldata params) external payable returns (uint256 amountIn);
}
```

Add the following helper functions:

```
function _prepareTradeFor(address trader) internal {
    uint256 approveAmount = 1000000000 ether;
    _writeTokenBalance(USDC, trader, approveAmount);
    _writeTokenBalance(SALE_TOKEN, trader, approveAmount);
    vm.startPrank(trader);
    IERC20(USDC).approve(UNIV3_ROUTER, approveAmount);
    IERC20(SALE_TOKEN).approve(UNIV3_ROUTER, approveAmount);
    vm.stopPrank();
}

function simulateTrade(bool fromUSDC, uint256 amount) internal {
    address contr = address(this);
    ISwapRouter swapRouter = ISwapRouter(UNIV3_ROUTER);
    address uniPool = IILOPoolImmutableState(iloPool)._cachedUniV3PoolAddress();

    ISwapRouter.ExactInputSingleParams memory exactInputParams = ISwapRouter.ExactInputSingleParams({
        tokenIn: USDC,
        tokenOut: SALE_TOKEN,
        fee: 500,
        recipient: address(this),
        deadline: block.timestamp + 1500,
        amountIn: amount,
        amountOutMinimum: 1,
        sqrtPriceLimitX96: 0
    });

    ISwapRouter.ExactOutputSingleParams memory exactOutputParams = ISwapRouter.ExactOutputSingleParams({
        tokenIn: SALE_TOKEN,
        tokenOut: USDC,
        fee: 500,
        recipient: address(this),
        deadline: block.timestamp + 1500,
        amountOut: amount,
        amountInMaximum: type(uint256).max,
        sqrtPriceLimitX96: 0
    });

    (uint256 fees0, uint256 fees1) = _getAccumulatedFees(uniPool);

    uint256 amount;
    if (fromUSDC) amount = swapRouter.exactInputSingle(exactInputParams);
    else amount = swapRouter.exactOutputSingle(exactOutputParams);

    (fees0, fees1) = _getAccumulatedFees(uniPool);
}
```

Finally, add the actual POC test that proves that subsequent calls to claim() fail if enough swap fees have been accumulated on the Uniswap pool:

```
function testClaimFailsOnTransfer() external {
    _launch();

    uint256 tokenIdInvestor = IILOPool(iloPool).tokenOfOwnerByIndex(INVESTOR, 0);
    uint256 tokenIdInvestor2 = IILOPool(iloPool).tokenOfOwnerByIndex(INVESTOR_2, 0);

    _prepareTradeFor(address(this));
    // to accumulate some Uniswap pool fees, we execute 100 trades of 10k each
    // the pool fee is 0.05% => this generates 5 * 100 in fees
    // total liq = 90k => fee = 0.05% = 5/trade => Inv1 fee = 11.11% of that amount (10k claimable from 90k) = 0.55 ...
    for (uint i = 0; i < 100; ++i) {
        //trade from USDC to VULT
        simulateTrade(true, 10000 ether);
        //trade from VULT to USDC
        simulateTrade(false, 10000 ether);
    }

    //unlocked the entire liquidity
    vm.warp(VEST_START_0 + (86400 * 2));

    console.log("******************************CLAIM FOR INVESTOR ************************************************");

    vm.prank(INVESTOR);
    try IILOPool(iloPool).claimNew(tokenIdInvestor) returns (uint256 amount0, uint256 amount1) {} catch Error(string memory reason) {
        console.log("Claim failed: ", reason);
    }

    console.log("******************************CLAIM FOR INVESTOR_2 ************************************************");

    uint256 inv2BalBefore = IERC20(USDC).balanceOf(INVESTOR_2) / 1 ether;
    console.log("INVESTOR_2 Balance USDC before claim: ", inv2BalBefore);

    vm.prank(INVESTOR_2);
    try IILOPool(iloPool).claimNew(tokenIdInvestor2) returns (uint256 amount0, uint256 amount1) {} catch Error(string memory reason) {
        //The ILOPool doesn't hold enough funds to execute the transfer to Investor_2
        console.log("! Claim failed: ", reason);
    }

    uint256 inv2BalAfter = IERC20(USDC).balanceOf(INVESTOR_2) / 1 ether;
    console.log("INVESTOR_2 Balance USDC after claim: ", inv2BalAfter);

    //The claim fails for Investor_2 => the balance before is the same as the balance after the claim call !!!
    assertEq(inv2BalAfter, inv2BalAfter);
}
```


## Tools Used

Manual Review

## Recommended Mitigation Steps

Modify the calculatio of the amounts that are sent to the feeTaker. It may also be necessary to re-structure the code of the claim() function (using structs), because there are already too many variables and adding addittional variable(s) may cause a "Stack too deep" error.

Add the following local variables to the claim() function:

```
uint256 amountFeeTaker0;
uint256 amountFeeTaker1;
```

Modify the following code section:

```diff
- (amount0, amount1) = _deductFees(amount0, amount1, _project.platformFee);
+ amountFeeTaker0 = FullMath.mulDiv(amount0, _project.platformFee, BPS);
+ amountFeeTaker1 = FullMath.mulDiv(amount1, _project.platformFee, BPS);
+ amount0 = amount0 - amountFeeTaker0;
+ amount1 = amount1 - amountFeeTaker1;
```

Also modify the following code section:

```diff
- (fees0, fees1) = _deductFees(fees0, fees1, _project.performanceFee);
+ amountFeeTaker0 += FullMath.mulDiv(fees0, _project.performanceFee, BPS);
+ amountFeeTaker1 += FullMath.mulDiv(fees1, _project.performanceFee, BPS);
+ fees0 -= FullMath.mulDiv(fees0, _project.performanceFee, BPS);
+ fees1 -= FullMath.mulDiv(fees1, _project.performanceFee, BPS);
```


Finally, modify the code that performs the transfer to the feeTaker at the end of the claim() function:

```diff
-- TransferHelper.safeTransfer(_cachedPoolKey.token0, feeTaker, amountCollected0 - amount0);
-- TransferHelper.safeTransfer(_cachedPoolKey.token1, feeTaker, amountCollected1 - amount1);
++ TransferHelper.safeTransfer(_cachedPoolKey.token0, feeTaker, amountFeeTaker0);
++ TransferHelper.safeTransfer(_cachedPoolKey.token1, feeTaker, amountFeeTaker1);
```

That way, we transfer only the amounts proportional to the burned liquidity and we leave enough funds on the ILOPool to handle subsequent claim() calls.



**********************************************************************************************************************************************************



# M01 Whitelist::setAllowedWhitelistIndex : Already whitelisted users may not be able to receive SALR tokens

## Risk: Medium

## Issue Type:

## Title:  Whitelist::setAllowedWhitelistIndex : Already whitelisted users may not be able to receive SALE tokens

## Links: https://github.com/code-423n4/2024-06-vultisig/blob/cb72b1e9053c02a58d874ff376359a83dc3f0742/hardhat-vultisig/contracts/Whitelist.sol#L180

## Impact

Let's assume, 100 addresses have been whitelisted for the ILO. Then, the Owner calls setAllowedWhitelistIndex() and specifies 80 for the newIndex parameters. Now, whenever a legitimately whitelisted user should receive SALE tokens, the _beforeTokenTransfer hook in the VultisigWhitelisted contract will be called and the checkWhitelist function in the Whitelist contract will be executed. 

This function executes the following code:

```
if (_allowedWhitelistIndex == 0 || _whitelistIndex[to] > _allowedWhitelistIndex) {
    revert NotWhitelisted();
} 
```

Now, because _allowedWhitelistIndex was changed from 100 to 80, the token transfer will fail for the last 20 users in the whitelist.


## Tools Used

Manual Review

## Recommended Mitigation Steps

Add the following code to the setAllowedWhitelistIndex function:

```
require(newIndex <= _whitelistCount);
```



